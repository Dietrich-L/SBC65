;AT28C16 R/W utility
;SBC65  program
;V1.0	27.02.23		original version

START	= $130

OUT	= $5000

VIA	= $6000		;6522 VIA
PORTB	= VIA
DDRB	= VIA+2
PORTA	= VIA+15
DDRA	= VIA+3

PCR	= VIA+12

;ROM calls
ROM	= $F800
PRTSTR	= ROM+9
DELAY	= ROM+12

;Constants


RAM_R	= %00000010	;WR 1 set CA2 high via ORA PCR
RAM_W	= %11111101	;WR 0 set CA2 low via AND PCR

RAM_OE	=  %01000000	;OE 1/ CE 0
RAM_CE	=  %00100000	;OE 0/ CE 1
RAM_DSEL = %01100000	;OE 1/ CE 1

EOT	= $00
CR	= $0D		; carriage return
LF	= $0A		; line feed

;ZP cells

ADR	= $B0
BUF	= $B2
ENDAD	= $B4
BYTE	= $B6
WRFLAG	= $B7		;bit 7=0 RD/ 1 WR


	ORG START

	JMP RD1PAGE
	JMP WR1PAGE

RD1PAGE	LDA #$00
	STA WRFLAG
	BEQ RW1PAGE

WR1PAGE	LDA #$80
	STA WRFLAG	

RW1PAGE	JSR INIT
	LDA BUF		;read 1 page
	STA ENDAD
	LDY BUF+1
	INY
	STY ENDAD+1
RWLOOP	BIT WRFLAG
	BMI RW1P1
	JSR RDBYTE	;read 1 byte
	JMP RW1P2

RW1P1	JSR WRBYTE
RW1P2	JSR NXTBYT
	BCC RWLOOP
	LDA #ENDM
	LDY #ENDM/256
	JMP PRTSTR


RDBYTE	LDA ADR
	STA OUT
	LDA ADR+1
	AND #%00011111	;only adr bits 8-12 & CS=0 & OE=0
	STA PORTB
	LDA #$00
	STA DDRA	;Port A is data input
	LDA PORTA
	LDY #0
	STA (BUF),Y
	LDA #RAM_DSEL
	STA PORTB
	RTS


WRBYTE	LDA ADR
	STA OUT
	LDA #$FF
	STA DDRA	;Port A is data output	
	LDY #0
	LDA (BUF),Y
	STA BYTE
	STA PORTA
	LDA ADR+1
	AND #%00011111	;only adr bits 8-12 & CS=0 & OE=0
	ORA #RAM_OE	;OE=1
	STA PORTB	;set adr
	PHA
	LDA PCR
	AND #RAM_W	;RW=0 
	STA PCR
	ORA #RAM_R	;RW=1
	STA PCR
	PLA
	AND #%00011111	;CS=0 & OE=0
	STA PORTB
	LDA #$00	;now read back until byte written
	STA DDRA
WRBYTE1	LDA PORTA
	CMP BYTE
	BNE WRBYTE1
	LDA #RAM_DSEL
	STA PORTB
	RTS


NXTBYT	INC ADR
	BNE NXTBYT1
	INC ADR+1
NXTBYT1	INC BUF
	BNE NXTBYT2
	INC BUF+1
NXTBYT2	LDA BUF+1
	CMP ENDAD+1
	BCC NXTBYTX
	LDA BUF
	CMP ENDAD
NXTBYTx	RTS


INIT	LDA #$7F	;PB7 input, PB6..PB0 output
	STA DDRB
	LDA #RAM_DSEL	;CE & OE =1
	STA PORTB
	LDA PCR
	ORA #RAM_R	;RW=1
	STA PCR
	RTS

ENDM	DB CR,LF,'AT28c64 ok',CR,LF,EOT

BUFFER

	END 